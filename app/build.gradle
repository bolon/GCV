buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.rere.fish.gcv"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "API_KEY_GCV", getPropertyFromPrivateFile("gcv_api_key")
        it.buildConfigField "String", "ENGINE_HOST_URL", getPropertyFromPrivateFile("engine_address")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8 //lambda ftw
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}


def getPropertyFromPrivateFile(String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("$projectDir/key"))

    System.out.println(properties.getProperty(property));

    return "\"" + properties.getProperty(property) + "\""
}

def supportLibraryVersion = '25.3.1'
def retrofit2Version = '2.3.0'
def gsonVersion = '2.7'
def iconifyVersion = '2.2.2'
def timberVersion = '4.5.1'
def daggerVersion = '1.2.5'
def okHttpVersion = '3.7.0'
def butterKnifeVersion = '8.5.1'

def cameraKitVersion = '0.9.17'
def permissionKarumiVersion = '4.1.0'

def googleApiClientVersion = '1.22.0'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofit2Version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit2Version"

    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    compile "com.jakewharton.timber:timber:$timberVersion"
    compile "com.joanzapata.iconify:android-iconify-fontawesome:$iconifyVersion"
    //try lottie pls nnd
    compile 'com.airbnb.android:lottie:2.0.0-rc1'

    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    compile group: 'com.google.code.gson', name: 'gson', version: "$gsonVersion"

    compile "com.squareup.dagger:dagger:$daggerVersion"
    provided "com.squareup.dagger:dagger-compiler:$daggerVersion"

    compile "com.karumi:dexter:$permissionKarumiVersion"

    compile "com.flurgle:camerakit:$cameraKitVersion"

    compile('com.google.apis:google-api-services-vision:v1-rev356-1.22.0') {
        exclude group: 'org.apache.httpcomponents'  //avoid conflict with android http version
        exclude group: 'com.google.code.findbugs'
    }
    compile "com.google.api-client:google-api-client-android:$googleApiClientVersion" exclude module: 'httpclient'
    compile "com.google.http-client:google-http-client-gson:$googleApiClientVersion" exclude module: 'httpclient'

    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.theartofdev.edmodo:android-image-cropper:2.4.+'

    compile 'org.parceler:parceler-api:1.1.8'
    annotationProcessor 'org.parceler:parceler:1.1.8'

    testCompile 'junit:junit:4.12'
}
